pipeline WorldBankpipeline {

  RetrieveExcel
    -> WBXLSXInterpreter
    -> WBSheetPicker
    -> WBColumnrename
    -> WBDataSelector;
  
  WBDataSelector
    -> BondTable
    -> BondData;
    
  WBDataSelector
    -> GDPTable
    -> GdpData;

  block RetrieveExcel oftype HttpExtractor {
    url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
  }

  block WBXLSXInterpreter oftype XLSXInterpreter { }

  block WBSheetPicker oftype SheetPicker {
   sheetName: "Figure S5.1.2";
  }

  block WBDataSelector oftype CellRangeSelector {
    select: range P2:S45;
  }

  block WBColumnrename oftype CellWriter {
    at: range P2:S2;
    write: [
      "Country Code",
      "Economy",
      "GDP per Capita",
      "Bond Issuance Share"
    ];
  }

  valuetype CountryCodeAlpha3 oftype text {
    constraints: [ CountryCodeRange ];
  }

  constraint CountryCodeRange on text: value matches /^[A-Z]{3}$/;

  valuetype gdp oftype decimal {
    constraints: [ gdpRange ];
  }
  constraint gdpRange on decimal:
    value > 0;

  valuetype Bond oftype decimal {
    constraints: [ BondRange ];
  }
  constraint BondRange on decimal:
    value >= 0 and value <= 1;

 
  block BondTable oftype TableInterpreter {
    header: true;
    columns: [
      "Country Code" oftype CountryCodeAlpha3,
      "Bond Issuance Share" oftype Bond
    ];
  }

  block GDPTable oftype TableInterpreter {
    header: true;
    columns: [
      "Country Code" oftype CountryCodeAlpha3,
      "GDP per Capita" oftype gdp
    ];
  }

  block BondData oftype SQLiteLoader {
    table: "bondIssuance";
    file: "./country-stats.sqlite";
  }

  block GdpData oftype SQLiteLoader {
    table: "gdpPerCapita";
    file: "./country-stats.sqlite";
  }
}
